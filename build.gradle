import io.franzbecker.gradle.lombok.task.DelombokTask

plugins {
  id 'java'
  id 'maven-publish'
  id 'io.franzbecker.gradle-lombok' version '4.0.0'
}

group = 'com.courtapi.aws'
version = '1.0.1'
description = 'aws-s3uri'
java.sourceCompatibility = JavaVersion.VERSION_1_10

repositories {
  mavenCentral()
}

lombok {
  version = "1.18.16"
}

// Generates delomboked sources for javadoc
task delombok(type: DelombokTask, dependsOn: compileJava) {
  ext.outputDir = file("$buildDir/delombok")
  outputs.dir(outputDir)
  sourceSets.main.java.srcDirs.each {
    inputs.dir(it)
    args(it, "-d", outputDir)
  }
  doFirst {
    outputDir.deleteDir()
  }
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
  useJUnitPlatform()
}

java {
  withSourcesJar()
  withJavadocJar()
}

javadoc {
  dependsOn delombok
  source = delombok.outputDir
  failOnError = false;
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

// Publish to CodeArtifact maven repo
// Note: you must get a CODEARTIFACT_AUTH_TOKEN from AWS, or just run with the script/publish tool
// which will fetch a token for you
publishing {
  repositories {
    maven {
      name "local"
      url "file://$projectDir/dist/maven/"
    }
    maven {
      name "codeartifact"
      url 'https://courtio-569840684722.d.codeartifact.us-east-1.amazonaws.com/maven/courtio-maven'
      credentials {
        username "aws"
        password System.env.CODEARTIFACT_AUTH_TOKEN
      }
    }
  }
  publications {
    maven(MavenPublication) {
      from(components.java)
    }
  }
}
